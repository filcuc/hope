set(BASE_TARGET_NAME hope)
set(TARGET_OBJECT_NAME "${BASE_TARGET_NAME}Objects")
set(TARGET_STATIC_NAME "${BASE_TARGET_NAME}Static")
set(TARGET_SHARED_NAME "${BASE_TARGET_NAME}")

set(TARGET_MAJOR 0)
set(TARGET_MINOR 1)
set(TARGET_PATCH 0)

file(GLOB_RECURSE TARGET_SOURCES *.cpp)
file(GLOB_RECURSE TARGET_HEADERS *.h)

# COMMON LIBRARIES
set(TARGET_LIBRARIES "")
if (UNIX OR MINGW)
    list(APPEND TARGET_LIBRARIES pthread)
endif()

# OBJECT FILES COMMON TO BOTH STATIC AND SHARED
include_directories(${CMAKE_CURRENT_SOURCE_DIR}/include)
add_library(${TARGET_OBJECT_NAME} OBJECT ${TARGET_SOURCES} ${TARGET_HEADERS})
set_target_properties(${TARGET_OBJECT_NAME} PROPERTIES
    POSITION_INDEPENDENT_CODE 1
)

# SHARED LIBRARY
add_library(${TARGET_SHARED_NAME} SHARED $<TARGET_OBJECTS:${TARGET_OBJECT_NAME}>)
target_link_libraries(${TARGET_SHARED_NAME} ${TARGET_LIBRARIES})
set_target_properties(${TARGET_SHARED_NAME} PROPERTIES
  SOVERSION "${TARGET_MAJOR}.${TARGET_MINOR}"
  VERSION "${TARGET_MAJOR}.${TARGET_MINOR}.${TARGET_PATCH}"
)

# STATIC LIBRARY
add_library(${TARGET_STATIC_NAME} STATIC $<TARGET_OBJECTS:${TARGET_OBJECT_NAME}>)
target_link_libraries(${TARGET_STATIC_NAME} ${TARGET_LIBRARIES})

# INSTALL DIRECTIVES
install(DIRECTORY include/hope DESTINATION include)
install(TARGETS ${TARGET_STATIC_NAME} ${TARGET_SHARED_NAME}
    RUNTIME DESTINATION bin
    LIBRARY DESTINATION lib
    ARCHIVE DESTINATION lib
)
